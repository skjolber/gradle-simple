// https://dzone.com/articles/publish-jar-artifact-using

plugins {
	id 'com.jfrog.artifactory' version '4.7.1'
	id 'java-library'
	id 'maven'
	id 'maven-publish'
}

group = 'com.github.skjolber'
version = currentVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile 'com.google.guava:guava:20.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

artifactory {
	publish {
		repository {
			repoKey = 'libs-release-local'
			maven = true
		}
		defaults {
			publications ('mavenJava')
		}
	}
}

task createPom {
    outputs.file "${buildDir}/pom.xml"
    inputs.property('group', project.group)
    inputs.property('name', project.name)
    inputs.property('pomVersion', version)

    doLast {
        pom {
            project {
                groupId inputs.properties['group']
                artifactId inputs.properties['name']
                version inputs.properties['pomVersion']
            }
         }.writeTo("${buildDir}/${project.name}-${project.version}.pom")
    }
}

artifactoryPublish {
    dependsOn createPom
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact(file("${buildDir}/${project.name}-${project.version}.pom"))
			artifact sourcesJar
			artifact javadocJar
		}
	}
}